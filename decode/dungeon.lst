   1  # ******************************************************************************************
      # ******************************************************************************************
      #
      #
      #        Dungeon Campaign by Robert Clardy. Reverse engineered by Paul Robson 2017
      #
      #
      # ******************************************************************************************
      # ******************************************************************************************
      Goto Initialise.769
   5  # ******************************************************************************************
      #                                    Delay two seconds
      # ******************************************************************************************
      For i = 1 To 1000
      Next i
   6  # ******************************************************************************************
      #                                      Delay a second
      # ******************************************************************************************
      For i = 1 To 500
      Next i
   7  # ******************************************************************************************
      #                                   Delay half a second
      # ******************************************************************************************
      For i = 1 To 500
      Next i
      Return
  10  Vlin yScreen,yScreen + 1 At xScreen
      Vlin yScreen,yScreen + 1 At xScreen + 1
      Return
  11  # ******************************************************************************************
      #           Convert xPos,yPos to position in maze, contents and screen position
      # ******************************************************************************************
      mazeIndex = level * 169 + xPos + 13 *(yPos - 1)
      xScreen = 3 * xPos - 2
      yScreen = 3 * yPos - 2
      mazeContents = maze(mazeIndex) / 10
      Return
  12  # ******************************************************************************************
      #                         Find an empty slot (upper digit is zero)
      # ******************************************************************************************
      xPos = Rnd(11) + 2
      yPos = Rnd(11) + 2
      Gosub Convertposition.11
      If mazeContents # 0 Then Findemptyslot.12
      Return
  13  # ******************************************************************************************
      #                    Find a square which is the upper left of open four
      # ******************************************************************************************
      Gosub Findemptyslot.12
      If maze(mazeIndex + 1) < - 5 Or maze(mazeIndex + 13) < - 5 Or maze(mazeIndex + 14) < - 5 Then Opensquareroom.13
      maze(mazeIndex) = 0
  14  # erase on the display
      Color= 0
      For j = 0 To 4
          Vlin yScreen,yScreen + 4 At xScreen + j
      Next j
  15  # on first two levels, make the first one a gas area (9x)
      If(level # 1 And level # 2) Or i # 1 Then Opensquarewalls.16
      For j = 0 To 1
          For mazeContents = 0 To 1
              maze(mazeIndex + j + 13 * mazeContents) = maze(mazeIndex + j + 13 * mazeContents) - 90
      Next mazeContents,j
  16  # remove walls to make a square. move to one of the 4 squares randomly.
      maze(mazeIndex + 1) = maze(mazeIndex + 1) + 2 *((maze(mazeIndex + 1) Mod 10) = - 2)
      maze(mazeIndex + 13) = maze(mazeIndex + 13) +((maze(mazeIndex + 13) Mod 10) = - 1)
      mazeIndex = mazeIndex + Rnd(2) + 13 *(Rnd(2))
      Return
  18  v = Peek(- 16384)
      If v < 128 Then 20
      Poke - 16368,0
      noGas = 1
      Return
  20  If noGas = 1 And nd # 3 Then 18
      If nd = 3 Then Gosub 40
      cr = cr + 1
      If(cr Mod(5 + 15 *(nd # 3))) # 0 Or noGas = 1 Then 18
  25  Tab 35
      gasTime = gasTime -(gasTime > 0)
      Print gasTime;
      If gasTime # 0 Or((cr Mod(10 + 30 *(nd # 3))) # 0) Then 18
  30  partySize = partySize - 1
      Print
      Print " one of your party died from the gas.";
      Gosub Delaytwosec.5
      If partySize <= 0 Then 8325
      Pop
      Goto 1500
  40  m = 1
      i = Rnd(2)
      sx = sx(1) + qx * i
      sy = sy(1) + qy *(i = 0)
  41  If sx < 1 Or sy < 1 Or sx > 38 Or sy > 38 Then 60
  42  n = Scrn(sx,sy)
      If n = colours(5) Then 50
      If n = colours(level + 1) Mod 16 Or n = colours(9) Then 60
      m = 1
      moveUp = Scrn(sx(8),sy(8))
      Color= moveUp +(8 *(moveUp # 0))
  45  Plot sx(8),sy(8)
      Color= 8 +(n *(n # 8))
      Plot sx,sy
      For i = 8 To 1 Step - 1
          sx(i) = sx(i - 1)
          sy(i) = sy(i - 1)
      Next i
      Return
  47  m = 1
      qx =(sx = 1) -(sx = 38)
      qy =(sy = 1) -(sy = 38)
      If qx = 0 Then qx = 2 * Rnd(2) - 1
      If qy = 0 Then qy = 2 * Rnd(2) - 1
      Goto 40
  50  Color= 8
      Plot sx,sy
      Gosub Delaysec.6
      Call - 936
      Print
      Print " the serpent ate one of your party!"
      Color= 0
      Gosub Delayhalfsec.7
  55  Plot sx,sy
      For i = 1 To 8
          Plot sx(i),sy(i)
          For j = 1 To 200
      Next j,i
      nd = 0
      partySize = partySize - 1
      Pop
      Goto 3750
  60  If m = 0 Then 47
      sx = sx - qx * i + qx *(i = 0)
      sy = sy - qy *(i = 0) + qy * i
      m = 0
      Goto 41
 500  moveDown = 0
      moveUp = 625
      Gosub 600
      moveUp = 620
      qx = qx + Sgn(xScreen - qx) -(yScreen = qy)
      qy = qy + Sgn(yScreen - qy) -(xScreen = qx)
      Gosub 600
      If moveDown = 1 Then 700
      dc = 0
      Return
 600  For i = - 1 To 2
          moveLeft = qy
          moveRight = qx + i
          Gosub moveUp
          moveLeft = qy + 1
          Gosub moveUp
      Next i
      For i = 0 To 1
          moveLeft = qy - 1
          moveRight = qx + i
          Gosub moveUp
          moveLeft = qy + 2
          Gosub moveUp
      Next i
      Return
 620  If Scrn(moveRight,moveLeft) = colours(5) Then moveDown = 1
      Color= colours(16)
      If Scrn(moveRight,moveLeft) = 0 Then Plot moveRight,moveLeft
      Return
 625  Color= 0
      If Scrn(moveRight,moveLeft) = colours(16) Then Plot moveRight,moveLeft
      Return
 700  Call - 936
      Print
      Print "the spectre got one of your party!"
      nd = 0
 705  moveUp = 620
      Gosub 600
      Color= colours(5)
      If Scrn(qx,qy) = colours(16) Then Plot qx,qy
      Color= colours(16)
      If Scrn(qx + 1,qy) = colours(5) Then Plot qx + 1,qy
      moveUp = 625
      Gosub 600
 707  qx = qx - 1
      If qx > 1 Then 705
      Plot qx + 1,qy
      partySize = partySize - 1
      Pop
      Goto 3750
 769  # ******************************************************************************************
      #
      #                                     Initialise Game
      #
      # ******************************************************************************************
      Rem
 770  # display title page (poke 50,63 is inverse poke 50,127 is normal)
      Poke 50,63
      Gosub Delaytwosec.5
      Vtab 21
      Tab 17
      Print " by "
      Print
      Tab 14
      Print "robert clardy"
      Gosub Delaytwosec.5
 777  Vtab 21
      Tab 11
      Print "copyright(c) 1979"
      Print
      Tab 9
      Print "by synergistic software"
      Gosub Delaysec.6
      Poke 50,255
 780  # ******************************************************************************************
      #                                  install machine code.
      # ******************************************************************************************
      Poke 2,173
      Poke 3,48
      Poke 4,192
      Poke 5,165
      Poke 6,0
      Poke 7,32
      Poke 8,168
      Poke 9,252
      Poke 10,165
      Poke 11,1
      Poke 12,208
 785  Poke 13,4
      Poke 14,198
      Poke 15,24
      Poke 16,240
      Poke 17,5
      Poke 18,198
      Poke 19,1
      Poke 20,76
      Poke 21,2
      Poke 22,0
      Poke 23,96
 790  Dim sx(8),sy(8),colours(16)
      sx = 0
      sy = 0
      partySize = 15
      strength = 1
      noGas = 1
      gasTime = 9
      colours(1) = 12
      colours(2) = 3
      colours(3) = 9
      colours(4) = 11
      colours(5) = 1
      colours(6) = 7
 795  colours(7) = 7
      colours(8) = 4
      colours(9) = 15
      colours(10) = 0
      colours(11) = 13
      colours(12) = 4
      colours(13) = 14
      colours(14) = 5
      colours(16) = 10
      Goto Createmaze.801
 801  # ******************************************************************************************
      #
      #                                     Create the maze
      #
      # ******************************************************************************************
      Rem
 802  # ******************************************************************************************
      #             dimension 13x13x4 maze and fill with '3' (wall right and below)
      # ******************************************************************************************
      Dim maze(676)
      For i = 1 To 676
          maze(i) = 3
      Next i
1001  # ******************************************************************************************
      #                                    Do for each level.
      # ******************************************************************************************
      For level = 0 To 3
          Gr
          Print
          Print
          Tab 8
          Print "this is the map for level ";level + 1
          Color= colours(level + 1)
1003  # Fill whole screen with the correct background colour (visuals only)
          For i = 0 To 39
              Vlin 0,39 At i
          Next i
          Color= 0
          Gosub Findemptyslot.12
          mazeClearCount = 121
1030  # ******************************************************************************************
      #                                 Find the start position
      # ******************************************************************************************
          Gosub Findemptyslot.12
          If maze(mazeIndex) < 0 Then 1030
          mazeClearCount = 121
1035  # ******************************************************************************************
      #        Clear the next square in the maze. When done set all non-open walls to -63
      # ******************************************************************************************
          If mazeClearCount # 1 Then Clearonesquare.1040
          Color= colours(level + 1)
          For mazeIndex = level * 169 + 1 To(level + 1) * 169
              If maze(mazeIndex) > 0 Then maze(mazeIndex) = - 63
1036      Next mazeIndex
          Goto Dorooms.1200
1040  # ******************************************************************************************
      #                            Clear a single square in the maze.
      # ******************************************************************************************
          moveRight = 0
          moveDown = 0
          moveLeft = 0
          moveUp = 0
          Gosub Convertposition.11
          Gosub 10
          maze(mazeIndex) = - Abs(maze(mazeIndex))
          mazeClearCount = mazeClearCount - 1
1050  # figure out which way we can go e.g. is there a wall there ?
          If xPos # 13 Then moveRight = maze(mazeIndex + 1) > 0
          If yPos # 13 Then moveDown = maze(mazeIndex + 13) > 0
          If xPos # 1 Then moveLeft = maze(mazeIndex - 1) > 0
          If yPos # 1 Then moveUp = maze(mazeIndex - 13) > 0
1080  # if there is a used square near by, find a new starting point
          mazeContents = moveRight + moveDown + moveLeft + moveUp
          If(mazeContents < 3 And Rnd(10) < 2) Or mazeContents = 0 Then Newstartpoint.1170
1110  # dispatch to movers
          Goto 1130 + 10 * Rnd(4)
1130  # try to move right
          If Not moveRight Then Trymovedirection.1110
          maze(mazeIndex) = maze(mazeIndex) + 1
          xPos = xPos + 1
          Vlin 3 * yPos - 2,3 * yPos - 1 At 3 *(xPos - 1)
          Goto Nextclear.1035
1140  # try to move down
          If Not moveDown Then Trymovedirection.1110
          maze(mazeIndex) = maze(mazeIndex) + 2
          yPos = yPos + 1
          Hlin 3 * xPos - 2,3 * xPos - 1 At 3 *(yPos - 1)
          Goto Nextclear.1035
1150  # try to move left
          If Not moveLeft Then Trymovedirection.1110
          maze(mazeIndex - 1) = maze(mazeIndex - 1) - 1
          xPos = xPos - 1
          Vlin 3 * yPos - 2,3 * yPos - 1 At 3 * xPos
          Goto Nextclear.1035
1160  # try to move up
          If Not moveUp Then Trymovedirection.1110
          maze(mazeIndex - 13) = maze(mazeIndex - 13) - 2
          yPos = yPos - 1
          Hlin 3 * xPos - 2,3 * xPos - 1 At 3 * yPos
          Goto Nextclear.1035
1170  # ******************************************************************************************
      #                    Find a new start point as we can't go any further.
      # ******************************************************************************************
          xPos = Rnd(13) + 1
          yPos = Rnd(13) + 1
          Gosub Convertposition.11
          If maze(mazeIndex) > 0 Then Newstartpoint.1170
          mazeClearCount = mazeClearCount + 1
          Goto Nextclear.1035
1200  # ******************************************************************************************
      #                  Create 5 square rooms. 3-5 have treasure in them (7x)
      # ******************************************************************************************
          For i = 1 To 5
              Gosub Opensquareroom.13
              If i > 2 Then maze(mazeIndex) = maze(mazeIndex) - 70
          Next i
          Poke 0,4
          Poke 24,8
          Call 2
      Next level
1205  # ******************************************************************************************
      #Put pits (2x) necro/ptero (3x) monster/w treasure (8x) stairs (5x) monster no treasure (4x)
      # ******************************************************************************************
      Rem
1210  For level = 0 To 3
          For i = 1 To 2
1212          Gosub Findemptyslot.12
              If maze(mazeIndex + 169 *(level < 3)) / 10 = - 6 Or maze(mazeIndex + 338 *(level < 2)) / 10 = - 6 Then 1212
              maze(mazeIndex) = maze(mazeIndex) - 20
              Gosub Findemptyslot.12
              maze(mazeIndex) = maze(mazeIndex) - 30
              Gosub Findemptyslot.12
              maze(mazeIndex) = maze(mazeIndex) - 80
1215      Next i
          For i = 1 To 3
              If level = 0 Then 1220
              Gosub Findemptyslot.12
              If maze(mazeIndex - 169) < - 5 Then 1215
              maze(mazeIndex) = maze(mazeIndex) - 50
1220      Next i
          Gosub Findemptyslot.12
          maze(mazeIndex) = maze(mazeIndex) - 40
      Next level
      level = 3
1260  # ******************************************************************************************
      #                                  Special stuff Level 4.
      # ******************************************************************************************
      Gosub Findemptyslot.12
      i = Rnd(2)
      j = Rnd(2)
      If i Then xPos = j * 13
      If Not i Then yPos = j * 13
      Gosub Convertposition.11
      If Abs(maze(mazeIndex) / 10) # 0 Then 1260
      wx = xPos
      wy = yPos
      level = 0
      Gosub Findemptyslot.12
1265  Gr
      Color= colours(5)
      Gosub 10
      Color= colours(1)
      Hlin 0,39 At 0
      Hlin 0,39 At 39
      Vlin 0,39 At 0
      Vlin 0,39 At 39
      Gosub 4010
1280  Dim a$(15),b$(250),c$(5)
1290  b$ = "lycanthropesgargoyles balrogs vampires goblins mighty ogresvicious orcscyclopses skeletons "
1292  b$(109) = "zombies evil trolls mummies huge spiderswerewolves minotaurs centaurs berserkers griffons "
1294  b$(217) = "basilisks gorgons "
1300  Goto 1560
1500  Call - 936
      Print
      Tab 10
      Print "what is your command?"
      Tab 5
      noGas =((maze(mazeIndex) / 10) # 9)
      If noGas = 0 Then Print "gas exposure time remaining = ";gasTime;
1501  Gosub 18
      If invisible = 15 Then 1520
      If nd = 0 Then Gosub 4250
      If nd = 2 Then Gosub 500
      If dc > 0 Then Gosub 7000
1520  If v = 210 Then 2000
      If v = 204 Then 2500
      If v = 213 Then 3000
      If v = 196 Then 3500
      If hasCarpet And v = 198 Then 3755
1530  If v = 211 Then 4310
      If v = 195 Then 8300
      If v = 202 Then 3700
      If v = 197 Then 6000
      If v = 216 Then 3740
      If invisible > 1 And v = 201 Then 3900
1560  Poke 50,63
      Print
      Print " legal commands are: ";
      Poke 50,255
      Tab 1
      If hasCarpet Then Print "f=fly ";
1565  Tab 32
      If invisible > 1 Then Print "i=invis ";
      If invisible = 0 Then Print
      Print "l=left u=up e=exit"
1570  Print "r=right s=search j=jump"
      Print "d=down x=status c=colors";
      Goto 1501
2000  If maze(mazeIndex) Mod 2 # 0 Then 5000
      dx = 1
      dy = 0
2020  If dwarfDead Then Gosub 4000
      For i = 1 To 3
          Color=(colours(13)) *((maze(mazeIndex) / 10) = 9)
          Gosub 10
          Color= invisible + colours(5)
          xScreen = xScreen + dx
          yScreen = yScreen + dy
          Gosub 10
      Next i
      xPos = xPos + dx
      yPos = yPos + dy
2100  Gosub Convertposition.11
      If mazeContents # - 9 Then 2120
      i = xPos
      j = yPos
      xPos = xPos - 1
      yPos = yPos - 1
2102  Gosub Convertposition.11
      If mazeContents = - 9 Then 2105
      xPos = xPos + 1
      If xPos = i Then 2102
      yPos = yPos + 1
      xPos = xPos - 2
      Goto 2102
2105  Color= colours(13)
      For moveUp = 0 To 4
          Vlin yScreen,yScreen + 4 At xScreen + moveUp
      Next moveUp
      Gosub 4005
      xPos = xPos + 1
      Gosub 4005
      yPos = yPos + 1
      Gosub 4005
      xPos = xPos - 1
      Gosub 4005
      xPos = i
      yPos = j
2110  Gosub Convertposition.11
      Color= invisible + colours(5)
      Gosub 10
2120  Gosub 4005
      If nd = 1 Then dc = dc + 1
      If mp # 0 Then 4550
      If Not v1 Then 2130
      v1 = 0
      Goto 1520
2130  j = 1
      mazeContents = Abs(mazeContents)
      If level # 0 And mazeContents = 5 Then 4110
      j = 0
      If level # 3 Then If Abs(maze(mazeIndex + 169) / 10) = 5 Then 4110
2145  If elfDead Or(level = 0 And yPos = 1) Or(level = 3 And yPos = 13) Then 2170
2150  For moveDown = - 1 To 1 Step 2
          For moveUp = 1 To 13 Step 12
              i = Abs(maze(mazeIndex + moveDown * moveUp) / 10)
              If i = 2 Or i = 3 Then 2160
      Next moveUp,moveDown
      Goto 2170
2160  Poke 50,127
      Call - 936
      Print
      Tab 10
      Print "beware! danger near!"
      Poke 50,255
      Gosub Delayhalfsec.7
2170  If mazeContents = 4 Or mazeContents = 8 Then 4510
      If nd = 1 And invisible # 15 And xPos = sx And yPos = sy Then 8000
      If mazeContents = 3 Then 4800
      If mazeContents = 2 Then 4910
      Goto 1500
2500  If xPos = 1 Then 5000
      If maze(mazeIndex - 1) Mod 2 # 0 Then 5000
      dx = - 1
      dy = 0
      Goto 2020
3000  If yPos = 1 Then 5000
      If(Abs(maze(mazeIndex - 13) Mod 10)) > 1 Then 5000
      dx = 0
      dy = - 1
      Goto 2020
3500  If(Abs(maze(mazeIndex) Mod 10)) > 1 Then 5000
      dx = 0
      dy = 1
      Goto 2020
3700  v1 = 1
      Call - 936
      Print
      Tab 15
      Print "direction?"
      Gosub 18
      Goto 1520
3740  Call - 936
      c$ = "alive"
      If elfDead Then c$ = "dead"
      Print "the elf is ";c$;". the dwarf is ";
      c$ = "alive"
      If dwarfDead Then c$ = "dead"
      Print c$;"."
3750  Print "your party now numbers ";partySize
      Print "total strength = ";partySize * strength
3751  Print "your treasure is worth ";s;" quadroons.";
      Goto 1501
3755  bx = 2 * Rnd(2) - 1
      by = 2 * Rnd(2) - 1
3760  i = Rnd(2)
      dx = bx *(i = 0)
      dy = by * i
3765  If(xPos = 13 And dx = 1) Or(yPos = 13 And dy = 1) Or(xPos = 1 And dx = - 1) Or(yPos = 1 And dy = - 1) Then 3755
      If(Abs(maze(mazeIndex + dx + 13 * dy)) / 10) = 6 Then 3755
3770  If i Then 3780
      For i = 1 To 3
          Color= Scrn(xScreen + dx +(dx = 1),yScreen) + colours(5)
          Vlin yScreen,yScreen + 1 At xScreen + dx +(dx = 1)
3771      Color= 16 + Scrn(xScreen +(dx # 1),yScreen) - colours(5)
          Vlin yScreen,yScreen + 1 At xScreen +(dx # 1)
          xScreen = xScreen + dx
      Next i
      xPos = xPos + dx
3775  Gosub Convertposition.11
      Gosub 4005
      If Peek(- 16384) < 128 Then 3760
      Poke - 16368,0
      hasCarpet = 0
      If mp Then 4555
      Goto 2130
3780  For i = 1 To 3
          Color= Scrn(xScreen,yScreen + dy +(dy = 1)) + colours(5)
          Hlin xScreen,xScreen + 1 At yScreen + dy +(dy = 1)
3781      Color= 16 + Scrn(xScreen,yScreen +(dy # 1)) - colours(5)
          Hlin xScreen,xScreen + 1 At yScreen +(dy # 1)
          yScreen = yScreen + dy
      Next i
      yPos = yPos + dy
      Goto 3775
3900  invisible = 15 *(invisible = 16)
      Color= invisible + colours(5)
      Gosub 10
      Goto 5050
4000  Color= colours(level + 1) *(colours(level + 1) > 16)
      Goto 4010
4002  maze(mazeIndex) = - Abs(maze(mazeIndex))
      Goto 4010
4005  Color= colours(level + 1)
      Gosub Convertposition.11
4010  p = maze(mazeIndex)
      If p > 0 And mazeContents # 5 Then Return
      If dwarfDead = 0 Then maze(mazeIndex) = Abs(p) + 10 *(p = 0)
      If xPos > 12 Then 4020
      If maze(mazeIndex + 1) > 0 And noGas Then 4020
4015  If p Mod 2 # 0 Then Vlin 3 *(yPos - 1),3 * yPos At 3 * xPos
4020  If xPos > 1 Then If maze(mazeIndex - 1) Mod 2 = - 1 Then Vlin 3 *(yPos - 1),3 * yPos At 3 *(xPos - 1)
4030  If yPos > 1 Then If(maze(mazeIndex - 13) Mod 10) < - 1 Then Hlin 3 *(xPos - 1),3 * xPos At 3 *(yPos - 1)
4040  If yPos > 12 Then Return
      If noGas And maze(mazeIndex + 13) > 0 Then Return
      If(p Mod 10) < - 1 Then Hlin 3 *(xPos - 1),3 * xPos At 3 * yPos
      Return
4110  Color= colours(9)
      i = Sgn(maze(mazeIndex))
      noGas = 0
      Gosub 4002
      If i > 0 Then maze(mazeIndex) = Abs(maze(mazeIndex))
      noGas = 1
      a$ = "down"
      If j Then a$ = "up"
4115  If j = 0 And level < 3 Then maze(mazeIndex + 169) = Abs(maze(mazeIndex + 169))
      If j And level Then maze(mazeIndex - 169) = Abs(maze(mazeIndex - 169))
4120  Call - 936
      Print
      Tab 7
      Print "stairway. go ";a$;" it(y/n)?"
4130  Gosub 18
      If v # 217 Then 2145
      level = level - j +(j = 0)
      For i = j * 8 + 8 To(j = 0) * 8 + 8 Step((j = 0) - j)
          Poke 0,i
          Poke 24,2
          Call 2
4145      For mazeContents = 1 To 70
      Next mazeContents,i
      Gosub Convertposition.11
      Call - 936
      Gosub 4150
      Goto 2130
4150  Gr
      Color= colours(5)
      Gosub 10
      mp = 0
      noGas = 0
      nd = 0
      sx = - 1
      sy = - 1
      If invisible = 15 Then invisible = 0
4160  Color= colours(level + 1)
      Vlin 0,39 At 0
      Vlin 0,39 At 39
      Hlin 0,39 At 0
      Hlin 0,39 At 39
      If colours(level + 1) > 15 Then 4420
4170  j = xPos
      moveUp = yPos
      For xPos = 1 To 13
          For yPos = 1 To 13
              Gosub Convertposition.11
              If maze(mazeIndex) <= 0 Then 4190
              Color= colours(level + 1)
              Gosub 4002
              maze(mazeIndex) = Abs(maze(mazeIndex))
4180          If level # 3 Then If(maze(mazeIndex + 169) / 10) = 5 Then mazeContents = 5
              Color= colours(mazeContents + 4)
              If mazeContents = 6 Then Color= colours(level + 1)
              If mazeContents = 5 Then Gosub 4002
              If mazeContents # 5 And mazeContents > 1 Then Gosub 10
4190  Next yPos,xPos
      noGas = 1
      xPos = j
      yPos = moveUp
      Gosub 4005
4220  Color= 0
      If level = 3 And(wy = 0 Or wy = 13) Then Hlin 3 * wx - 2,3 * wx - 1 At 3 * wy
      If level = 3 And(wx = 0 Or wx = 13) Then Vlin 3 * wy - 2,3 * wy - 1 At 3 * wx
      Return
4250  dc = 0
      If Rnd(30) > 0 Then Return
      If level < 2 Then 4260
      If level = 2 Then 4256
      nd = 2
      moveUp = 620
      qx = 36
      qy = 36
      Gosub 600
      Poke 50,63
      Call - 936
      For i = 1 To 83
          Print " ";
      Next i
4255  Print "a spectre has entered the dungeon";
      For i = 1 To 83
          Print " ";
      Next i
      Poke 50,255
      Goto 4259
4256  moveUp = xPos
      moveDown = yPos
      qx = 1
      qy = 1
      Gosub Findemptyslot.12
      nd = 3
      Color= 8
      Plot xScreen,yScreen
      sx(8) = xScreen
      sy(8) = yScreen
      For i = 8 To 2 Step - 1
4257      dy = 0
          dx = Rnd(3) - 1
          If dx = 0 Then dy = 2 * Rnd(2) - 1
          If Scrn(sx(i) + dx,sy(i) + dy) = colours(level + 1) Mod 16 Then 4257
          sx(i - 1) = sx(i) + dx
4258      sy(i - 1) = sy(i) + dy
          Plot sx(i - 1),sy(i - 1)
          For j = 1 To 100
      Next j,i
      Call - 936
      Print
      Print "a giant serpent has entered the dungeon!"
      xPos = moveUp
      yPos = moveDown
4259  Gosub Delaysec.6
      Gosub Convertposition.11
      Return
4260  nd = 1
      qx = xPos
      qy = yPos
4265  Gosub Findemptyslot.12
      If Abs(xPos - qx) + Abs(yPos - qy) < 6 Then 4265
      Color= colours(14)
      Gosub 10
      sx = xPos
      sy = yPos
      xPos = qx
      yPos = qy
      qx = xScreen
      qy = yScreen
      Gosub Convertposition.11
4267  Call - 936
      Print
      Poke 50,127
      Print "danger!!!";
      Poke 50,63
      Print " you woke a man-eating dragon!"
      Poke 50,255
4270  For i = 1 To 70
          moveDown = Peek(- 16336)
          For j = 1 To 3
      Next j,i
      Goto 4259
4310  Gosub Convertposition.11
      If mazeContents = 7 Or mazeContents = 8 Then 4325
      Call - 936
      Print
      Tab 10
      Print "sorry, no treasure here.";
      Goto 1501
4325  For j = 1 To 8
          moveDown = j Mod 2
          Color= colours(5 + 6 * moveDown)
          Gosub 10
          For moveLeft = 1 To 50
          Next moveLeft
          Poke 0,8 - moveDown
          Poke 24,2
          Call 2
      Next j
      maze(mazeIndex) = maze(mazeIndex) - 40 -(30 *(mazeContents = 7))
4328  moveLeft = 100 *(Rnd(10) + 2 + 4 * level)
4330  s = s + moveLeft
      Call - 936
      Print "you've found ";moveLeft;" quadroons worth of"
      Print "gold and jewels.";
      If Rnd(2) Then 1501
4335  Print " you've also found "
      Goto 4370 + 10 * Rnd(8)
4345  i = xPos
      j = yPos
      For xPos = 1 To 13
          For yPos = 1 To 13
              Gosub Convertposition.11
              mazeContents = Abs(mazeContents)
              If mazeContents # moveUp And mazeContents # moveDown Then 4346
              Color= colours(moveUp + 4)
              Gosub 10
              Gosub 4005
4346  Next yPos,xPos
      Goto 5045
4370  Color= colours(9)
      i = xPos
      j = yPos
      For xPos = 1 To 13
          For yPos = 1 To 13
              Gosub Convertposition.11
              If level # 0 And Abs(mazeContents) = 5 Then Gosub 4010
4375          If level = 3 Then 4377
              mazeContents = Abs(maze(mazeIndex + 169) / 10)
              If mazeContents # 5 Then 4377
              Gosub 4010
              maze(mazeIndex + 169) = Abs(maze(mazeIndex + 169))
4377  Next yPos,xPos
      Goto 5045
4380  moveUp = 7
      moveDown = 8
      Goto 4345
4390  moveUp = 4
      moveDown = 8
      Goto 4345
4400  moveUp = 2
      moveDown = 3
      Goto 4345
4410  wp = 1
      Print "a magic sword(can be used only once).";
      Goto 5050
4420  i = xPos
      j = yPos
      colours(level + 1) = colours(level + 1) + 16
      For xPos = 1 To 13
          For yPos = 1 To 13
              Gosub Convertposition.11
              moveDown =(mazeContents >= 0) -(mazeContents < 0)
              Gosub 4005
              If Abs(mazeContents) = 6 Then Gosub 10
              maze(mazeIndex) = moveDown * maze(mazeIndex)
4425  Next yPos,xPos
      Goto 5045
4430  Print "a magic carpet."
      hasCarpet = 1
      Goto 5050
4440  Print "an invisibility potion!";
      invisible = 16
      Goto 5050
4510  xScreen = xScreen - 3 * dx
      yScreen = yScreen - 3 * dy
      mx = xScreen
      my = yScreen
      Color= colours(8)
      Gosub 10
      mp = 1
      tx = xPos
      ty = yPos
4521  mstr = 15 + Rnd(10) + level * 20
      i = Rnd(20) * 12 + 1
      a$ = b$(i,i + 11)
      Call - 936
      Print "you've come upon ";Rnd(19) + 5;" ";a$
4522  Gosub Convertposition.11
      Print "their strength is ";mstr;" while yours"
      Print "is ";partySize * strength;". what is your command?"
      Goto 1501
4550  If xScreen = mx And yScreen = my And invisible # 15 Then 4710
      If(xScreen = mx Or yScreen = my) And invisible # 15 Then 4560
4555  Color= colours(8)
      xScreen = 3 * tx - 2
      yScreen = 3 * ty - 2
      Gosub 10
      Color= 0
      xScreen = mx
      yScreen = my
      Gosub 10
      Gosub Convertposition.11
      mp = 0
      Goto 2130
4560  xScreen = mx
      yScreen = my
      For i = 1 To 6
          Color= 0
          Gosub 10
          Color= colours(8)
          xScreen = xScreen + dx
          yScreen = yScreen + dy
          Gosub 10
      Next i
      Gosub Convertposition.11
      Goto 4710
4710  For i = 1 To 6
          Poke 0,20
          Poke 24,2
          Color= colours(8)
          Gosub 10
          For moveLeft = 1 To 100
          Next moveLeft
          Color= colours(5)
          Gosub 10
          Call 2
      Next i
      i = level * 169 + tx + 13 *(ty - 1)
4711  maze(i) = maze(i) - 10 - 30 *((maze(i) / 10) = 4)
      Color= colours(8)
      Plot xScreen + 1,yScreen
      Plot xScreen,yScreen + 1
      If wp = 0 Then 4713
      Call - 936
      Print
      Tab 6
4712  Print "use your magic sword(y/n)?";
      Gosub 18
      If v = 217 Then 4750
4713  Call - 936
      Print "battle has begun. hit space bar to roll dice for your attack. hit it again to stop dice.";
      mazeContents = 0
      j = 0
      Gosub 18
4716  i = Rnd(10) + 1
      Print i
      If Peek(- 16384) < 128 Then 4716
      moveLeft =(partySize * strength * i) > mstr * 3
      Poke - 16368,0
4718  Print "you rolled ";i;"."
      If moveLeft Then Print "you got a hit!"
      If Not moveLeft Then Print "no hit."
      j = j + moveLeft
4719  Print "now roll for the enemy.";
      Gosub 18
4721  i = Rnd(10) + 1
      Print i
      If Peek(- 16384) < 128 Then 4721
      Poke - 16368,0
      mazeContents = mazeContents +(i > 7)
4722  Print
      Print "the enemy gets a ";i
      Print "you've lost ";mazeContents;" of your party."
      Gosub Delaysec.6
4723  If mazeContents >= partySize Then 8325
      If j = 2 Then 4730
      c$ = "none"
      If j Then c$ = "half"
      Print c$;" of the ";a$;" are dead."
4724  If mazeContents > 3 Then mstr = mstr / 2
4725  Print "roll again for your side.";
      Gosub 18
      Goto 4716
4730  partySize = partySize - mazeContents
      strength = strength + 1
      If mazeContents And level And Not elfDead And Rnd(15) > partySize Then 4740
      If mazeContents And level = 2 And Not dwarfDead And Rnd(15) > partySize Then 4741
4731  Print
      Print "the ";a$;" were all killed."
      mp = 0
      Goto 3750
4740  elfDead = 1
      Print
      Print "the elf was killed. no more warnings."
      Gosub Delaytwosec.5
      Goto 4731
4741  dwarfDead = 1
      Print
      Print "the dwarf was killed. no more map."
      Gosub Delaytwosec.5
      Goto 4731
4750  wp = 0
      strength = strength + mstr / partySize
      Print
      Print "the magic sword killed the ";a$
      Color= colours(5)
      Gosub 10
      mp = 0
      Goto 3750
4800  Call - 936
      If Rnd(2) Then 4801
      Print "you disturbed an evil necromancer who sent you here."
      Goto 4802
4801  Print "you were grabbed by pteridactyls, flown here and left."
4802  i = 20
      For j = 1 To 15
          Poke 0,i
          Poke 24,2
          i = i - 1
          Call 2
      Next j
      Poke 0,22
      Poke 24,2
      Call 2
4805  level = Rnd(3)
      Gosub Findemptyslot.12
      Gosub 4150
      Goto 2130
4910  Call - 936
      Print
      Poke 50,127
      Tab 14
      Print "pit trap!!!"
      Poke 50,255
      For i = 1 To 4
          Color= 1 +(i Mod 2) * 10
          Gosub 10
          For j = 1 To 6
              Poke 0,12
              Poke 24,2
4920          Call 2
      Next j,i
      If level = 3 Then 4950
      level = level + Rnd(2) + 1
      If level > 3 Then level = 3
      Tab 8
      Print "you've fallen to level ";level + 1
4942  Gosub Convertposition.11
      Gosub 4150
      Goto 2100
4950  i = 1 + Rnd(2)
      partySize = partySize - i
      If partySize <= 0 Then 8325
      Print i;" of your men fell into the bottomless"
4951  Print "pit. there are only ";partySize;" of you left.";
      Goto 1501
5000  Poke 0,15
      Poke 24,2
      Call 2
      Goto 2100
5045  xPos = i
      yPos = j
      Gosub Convertposition.11
      If noGas = 0 Then 4170
      Print "this map!"
5050  p = 9
      For i = 1 To 8
          Poke 0,p
          Poke 24,2
          Call 2
          p = p - 1
      Next i
      Goto 1501
6000  If Not(level = 3 And(xPos = wx Or xPos = wx + 1) And(yPos = wy Or yPos = wy + 1)) Then 5000
      maze(1) = 10
      maze(2) = 3
      maze(3) = 10
      maze(4) = 3
      maze(5) = 9
      maze(6) = 3
      maze(7) = 7
6005  maze(8) = 8
      For j = 1 To 3
          If j = 3 Then maze(8) = 16
          For i = 1 To 7 Step 2
              Poke 0,maze(i)
              Poke 24,maze(i + 1)
              Call 2
      Next i,j
6006  Text
      Call - 936
      Vtab 10
      Print "you made it!!! you got out of the dun- geon with ";s;" quadroons worth of gold"
6010  Print "and jewels! only ";partySize;" of your party sur-"
      Print "vived though."
      Print "come again soon."
      End
7000  t = level * 169 + sx + 13 *(sy - 1)
      moveRight =(Abs(maze(t) Mod 2) = 0)
      moveLeft =(Abs(maze(t - 1) Mod 2) = 0)
      moveDown =(Abs(maze(t) Mod 10) < 2)
      dc = - 1
      moveUp = 0
      If level = 0 And yPos = 1 Then 7005
7002  moveUp =(Abs(maze(t - 13) Mod 10) < 2)
7005  dx =(xPos > sx) * moveRight -(xPos < sx) * moveLeft
      dy =(yPos > sy) * moveDown -(yPos < sy) * moveUp
      i = Rnd(2)
      If dx # 0 Or dy # 0 Then 7018
      dx = moveRight - moveLeft
      dy = moveDown - moveUp
      If ct < 4 Then 7018
7015  dx = Sgn(xPos - sx)
      dy = Sgn(yPos - sy)
7018  If dx # 0 And dy # 0 Then dx = dx * i
      If dx # 0 And dy # 0 Then dy = 0
      If 2 * dx + 3 * dy # - lm Then ct = - 1
      ct = ct + 1
      lm = 2 * dx + 3 * dy
      moveUp = xScreen
      moveRight = yScreen
7020  xScreen = qx
      yScreen = qy
      For i = 1 To 3
          Color= 0
          Gosub 10
          Color= colours(14)
          xScreen = xScreen + dx
          yScreen = yScreen + dy
          Gosub 10
      Next i
      qx = xScreen
      qy = yScreen
      sx = sx + dx
      sy = sy + dy
      xScreen = moveUp
      yScreen = moveRight
7030  If xPos # sx Or yPos # sy Then Return
      Pop
8000  For i = 1 To 6
          Color= 1 +(i Mod 2) * 4
          Gosub 10
          For j = 1 To 8
              Poke 0,5
              Poke 24,2
              Call 2
      Next j,i
      nd = 0
      sx = - 1
      sy = - 1
      a$ = "dragon"
      If wp Then 4750
8002  If partySize <= 2 Then 8325
      partySize = partySize - 2
      Call - 936
      Print "the dragon ate two of your party."
      Goto 3750
8300  dc = - 1
      Poke 50,63
      Call - 936
      Print " color codes for level ";level + 1;" are: ";
      Poke 50,255
8305  If level = 0 Then Print "l.green";
      If level = 1 Then Print "orange";
      If level = 2 Then Print "violet";
      If level = 3 Then Print "flesh";
      Print "=walls";
8307  Tab 16
      Print "l.blue=haz red=you yellow=treas. ";
      If level = 0 Or level = 1 Then Print "grey=dragon";
      If level = 2 Then Print "brown=snake";
8310  If level = 3 Then Print "grey=spect";
      Tab 29
      Print "white=stairsd.green=monst ";
      If level = 1 Or level = 2 Then Print "olive=gas";
      Goto 1501
8325  Text
      Call - 936
      Vtab 10
      Print "your expedition had no survivors. con- dolences will be sent to next of kin."
      End
